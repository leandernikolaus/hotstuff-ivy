#lang ivy1.7

#TODO: Model certificates

module total_order(t) = {
    property [Transitivity] X:t < Y & Y < Z -> X < Z  
    property [antisymmetry] ~(X:t < Y & Y < X)
    property [totality] X:t < Y | X=Y | Y < X
}

isolate heights = {
    type t
    # individual min:t

    specification {
        instantiate total_order(t)
        property [minimum] 0 < H:t | 0 = H:t
    }
    implementation {
        interpret t -> nat
    }
}

isolate tree = {
    type block

    individual genesis:block

    #assign to every block its block height
    function block_height(B:block):heights.t
    #assign to every block a predecessor
    relation prev(B:block,C:block)
    relation proposed(B:block)

    relation ancestor(B:block, C:block)

    #init
    after init {
        prev(B,C) := false;
        prev(genesis,genesis) := true;
        proposed(B) := B = genesis;
        block_height(B) := 0;
        ancestor(B,C) := B=C;
    }

    action propose(b_new:block,b_prev:block,h:heights.t) = {
        #these are assume, not require, to avoid repeting them in hotstuff
        require ~proposed(b_new);
        require proposed(b_prev);
        require block_height(b_prev) < h;

        #create tree
        prev(b_new,b_prev) := true;
        block_height(b_new) := h;

        #mark as proposed
        proposed(b_new) := true;

        ancestor(B,b_new) := ancestor(B,b_prev) | B=b_prev | B = b_new;
    }

    #export propose

    specification {
        invariant prev(genesis,genesis)
        invariant prev(B,C) -> ancestor(C,B)
        invariant prev(D,C) & ancestor(B,C) -> ancestor(B,D)
        invariant ~proposed(B) -> ~prev(B,C)
        invariant ~proposed(B) -> block_height(B) = 0
        invariant prev(B,C) & prev(B,D) -> C=D
        invariant prev(B,C) & prev(C,B) -> C=tree.genesis
        invariant prev(B,C) & B ~= C -> block_height(C) < block_height(B)
        invariant prev(B,C) -> proposed(C)
        invariant proposed(B) -> ancestor(genesis,B)
        invariant prev(B,C) -> ancestor(C,B)
        invariant ancestor(B,C) & B ~= C -> block_height(B) < block_height(C)
        invariant ancestor(B,C) & ~proposed(B) -> B=C
        invariant ancestor(B,B)
        invariant ancestor(B,C) & ancestor(C,B) -> B=C
        invariant ancestor(B,C) & ancestor(C,D) -> ancestor(B,D)
        invariant ancestor(B,D) & ancestor(C,D) -> ancestor(B,C) | ancestor(C,B)
        invariant ancestor(B,D) & ancestor(C,D) &  ancestor(B,C) & B ~= C & C ~= D -> ~prev(D,B)

        #invariant proposed(B) -> exists D. prev(B,D)
        #invariant prev(B,C) -> exists D. prev(B,D)
        #invariant ancestor(B,C) & B~=C -> exists D. prev(C,D) & ancestor(B,D)
    }

} with heights

isolate base = {
    type node
    type quorum

    relation member(N:node, Q:quorum)

    relation is_good(N:node) # should be used only in the proof!
    axiom forall Q. exists N. is_good(N) & member(N,Q)
    axiom forall Q1,Q2. exists N. is_good(N) & member(N, Q1) & member(N, Q2)


    #this shows that node N signed block B at height H
    relation voted(N:node,B:tree.block)

    function max(N:node):heights.t
    function lock(N:node):heights.t

    relation certified(B:tree.block)
    definition certified(B) = B=tree.genesis | exists Q. forall N. member(N,Q) -> voted(N,B)

    relation confirm(B:tree.block,C:tree.block,D:tree.block)
    definition confirm(B,C,D) = tree.prev(C,B) & tree.prev(D,C) & 
                                (forall H. H < tree.block_height(D) -> H = tree.block_height(C) | 
                                                                       H <= tree.block_height(B))

    
    #init
    after init {
        voted(N,B) := B = tree.genesis;
        max(N) := 0;
        lock(N) := 0;
    } 

    action propose(b_new:tree.block,b_prev:tree.block,h:heights.t) = {
        require ~tree.proposed(b_new);
        require tree.block_height(b_prev) < h;
        require certified(b_prev);
        
        #create tree
        call tree.propose(b_new,b_prev,h);
    }

    action vote(n:node, b:tree.block, p:tree.block, gp:tree.block) = {
        require tree.proposed(b);
        require tree.prev(b,p) & tree.prev(p,gp);

        #RULE 1 only applies to good nodes
        require ~is_good(n) | max(n) < tree.block_height(b);

        #RULE 2 original hotstuff_proof safety and liveness rule
        #require is_good(n) & lock(n,l) -> tree.ancestor(l,b) | tree.block_height(l) < tree.block_height(j);
        #RULE 2s simplified rule
        require is_good(n) -> lock(n) <= tree.block_height(p);


        if lock(n) < tree.block_height(gp) {
            lock(n) := tree.block_height(gp);
        };

        voted(n,b) := true;
        max(n) := tree.block_height(b);
    }
    

    #export propose
    #export vote

    specification {
        invariant voted(N,B) & tree.prev(B,J) & tree.prev(J,JJ) -> tree.block_height(JJ) <= lock(N) 
        #invariant base.is_good(N) & lock(N,B) -> tree.block_height(B) <= base.max(N)
        #invariant base.is_good(N) & lock(N,B) & tree.block_height(B) = base.max(N) -> B = tree.genesis
        


        #invariant certified(B) -> forall Q. exists N. member(N,Q) & is_good(N) & voted(N,B)
        invariant voted(N,B) -> tree.proposed(B)
        invariant voted(N,B) & is_good(N) -> tree.block_height(B) <= max(N)
        invariant voted(N,B) & voted(N,C) & B ~= C & is_good(N) -> tree.block_height(B) ~= tree.block_height(C)
        invariant tree.prev(B,C) -> certified(C)
        invariant certified(B) & certified(C) & B ~=C -> tree.block_height(B) ~= tree.block_height(C)
    }

} with heights, tree